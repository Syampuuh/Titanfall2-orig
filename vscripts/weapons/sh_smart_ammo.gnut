untyped

global function SmartAmmo_Init

/****************************************************/
/*				Mega Weapon Functions				*/
/****************************************************/

// Called from mega weapon script callbacks
global function SmartAmmo_FireWeapon

/********************************/
/*	 Global Utility Functions	*/
/*	Call these from any script	*/
/********************************/

// Behavior Option Functions
global function SmartAmmo_SetAllowUnlockedFiring
global function SmartAmmo_SetAimAttachment				// Set a custom attachment name for target lockon
global function SmartAmmo_SetMissileSpeed					// Set the speed the missile will travel when it is first created
global function SmartAmmo_SetMissileSpeedLimit		// Sets the max speed for the missile. After being created it will accelerate up to this speed
global function SmartAmmo_SetMissileHomingSpeed		// Set the turning 'homing' rate of the missile
global function SmartAmmo_SetMissileShouldDropKick
global function SmartAmmo_SetUnlockAfterBurst			// Makes the targets get cleared after the burst fire is complete. Default is false
global function SmartAmmo_SetMissileAimAtCursor				// Makes missiles fly towards where the crosshair is aimed, instead of parallel to the crosshair direction. Default is false.
global function SmartAmmo_SetWarningIndicatorDelay
global function SmartAmmo_SetDisplayKeybinding
global function SmartAmmo_SetExpandContract
global function SmartAmmo_EntHasEnoughTrackedMarks
global function SmartAmmo_SetShouldTrackPosition

/****************************************************/
/*				Interal Get/Set Functions			*/
/*	( not meant to be called outside smart script )	*/
/****************************************************/
global function SmartAmmo_GetUseAltAmmoFunc
global function SmartAmmo_GetAllowUnlockedFiring
global function SmartAmmo_SetWeaponFireFailedTime
global function SmartAmmo_GetWeaponFireFailedTime
global function SmartAmmo_GetAimAttachment
global function SmartAmmo_GetMissileSpeed
global function SmartAmmo_GetMissileSpeedLimit
global function SmartAmmo_GetMissileHomingSpeed
global function SmartAmmo_GetMissileShouldDropKick
global function SmartAmmo_GetUnlockAfterBurst
global function SmartAmmo_GetMissileAimAtCursor
global function SmartAmmo_GetWarningIndicatorDelay
global function SmartAmmo_GetDisplayKeybinding
global function SmartAmmo_GetExpandContract
global function SmartAmmo_GetShouldTrackPosition

global function SmartAmmo_SetMissileTarget
global function SmartAmmo_TransferMissileLockons
global function SmartAmmo_CanWeaponBeFired

global function SmartAmmo_WeaponRequiresTrackedMarks
global function SmartAmmo_GetWeaponTargets

#if CLIENT
//	global function ClientCodeCallback_OnPredictedEntityRemove
#endif

global const SMART_AMMO_DEFAULT_AIM_ATTACHMENT = "CHESTFOCUS"
global const SMART_AMMO_AI_AIM_ATTACHMENT = "HEADSHOT"
global const SMART_AMMO_GRENADE_AIM_ATTACHMENT = "LIGHT" //Satchels have LIGHT others grenades default to worldspace.
global const TARGET_SET_FRACTION = 0.0001 //This target.fraction is set so the target is not seen as new.
global const HOMING_SFX_LOOP	= "Weapon_Sidwinder_Projectile"
global const SMART_AMMO_PLAYER_MAX_LOCKS			= 3

typedef FireWeaponCallback int functionref( entity, WeaponPrimaryAttackParams, int, int, bool, entity )
table< string, FireWeaponCallback > VALID_WEAPON_TYPES = {
	bullet 					= null,
	homing_missile 			= null,
	sniper					= null,
}

function SmartAmmo_Init()
{
	VALID_WEAPON_TYPES.bullet			= SmartAmmo_FireWeapon_Bullet
	VALID_WEAPON_TYPES.homing_missile	= SmartAmmo_FireWeapon_HomingMissile

	#if SERVER
		AddDamageCallbackSourceID( eDamageSourceId.mp_weapon_smart_pistol, SmartPistol_DamagedPlayerOrNPC )
	#endif
}

/******************************************/
/* 	GLOBAL - CALLED ON CLIENT AND SERVER  */
/******************************************/

int function SmartAmmo_FireWeapon( entity weapon, WeaponPrimaryAttackParams attackParams, int damageType, int explosionDamageType )
{
	if ( !IsValid( weapon ) )
		return 0

	if ( !weapon.SmartAmmo_IsEnabled() )
	{
		if ( !SmartAmmo_GetAllowUnlockedFiring( weapon ) )
		{
			SmartAmmo_SetWeaponFireFailedTime( weapon )
			return 0
		}
	}

	#if CLIENT
		if ( IsWatchingReplay() )
			return 0 // temp fix for SmartAmmo_GetStoredTargets not returning the same list during kill replay
	#endif

	array<entity> storedTargets = weapon.SmartAmmo_GetStoredTargets()
	bool weaponRequiresTrackedMarks = SmartAmmo_WeaponRequiresTrackedMarks( weapon )
	if ( weaponRequiresTrackedMarks )
		RemovePartiallyTrackedEnts( weapon, storedTargets )
	var targets = weapon.SmartAmmo_GetTargets()
	//TODO: Update with cleaner tracking count method.
	local targetFracs = {}
	local targetActiveShots = {}

	foreach ( target in targets )
	{
		if ( target.fraction < 1.0 )
			continue

		targetFracs[target.ent] <- target.fraction
		targetActiveShots[target.ent] <- target.activeShot
	}

	if ( attackParams.burstIndex == 0 )
	{
		// The first time we fire the weapon we store off all targets we are locked onto so we can iterate through them with each burst shot.
		// The reason we have to store them off is because if burst bullet 1 kills the first target they will be removed from the smart targets array
		// when the second burst bullet is fired, and when we try to shoot target '2' we will actually be shooting target '3' becuase index 0 is removed
		// and all targets shift in the array

		weapon.SmartAmmo_StoreTargets()
		//printt( "Storing", weapon.SmartAmmo_GetStoredTargets().len(), "targets" )
		//PrintTable( weapon.SmartAmmo_GetStoredTargets() )
		storedTargets = weapon.SmartAmmo_GetStoredTargets()
		if ( weaponRequiresTrackedMarks )
			RemovePartiallyTrackedEnts( weapon, storedTargets )
		// figure out how many targets to hit with one pull of the trigger
		local maxTargetedBurst = SmartAmmo_GetMaxTargetedBurst( weapon )

		local alwaysDoBurst = weapon.GetWeaponSettingBool( eWeaponVar.smart_ammo_always_do_burst )
		local burstCount = 0
		if ( weapon.GetWeaponSettingBool( eWeaponVar.smart_ammo_alt_lock_style ) )
		{
			local chargeFrac = 1 - weapon.GetWeaponChargeFraction()
			local shotFrac = 1 / maxTargetedBurst.tofloat()

			if ( chargeFrac < shotFrac )
				return 0
		}

		foreach ( target in storedTargets )
		{
			Assert( target in targetFracs )

			burstCount += floor( targetFracs[target] )
		}

		if ( alwaysDoBurst )
			burstCount = maxTargetedBurst

		burstCount = min( maxTargetedBurst, burstCount )

		if ( weapon.GetWeaponInfoFileKeyField( "smart_ammo_doBurstPerTarget" ) == 1 )
			burstCount *= storedTargets.len()

		if ( burstCount <= 0 )
			burstCount = 0

		weapon.SetWeaponBurstFireCount( burstCount )
	}

	// We don't have any targets locked. Should we do unlocked fire?
	if ( storedTargets.len() == 0 && !SmartAmmo_GetAllowUnlockedFiring( weapon ) )
	{
		SmartAmmo_SetWeaponFireFailedTime( weapon )
		return 0
	}

	array<entity> expandedStoredTargets = []

	foreach ( target in storedTargets )
	{
		local burstCount

		if ( target in targetFracs )
			burstCount = floor( targetFracs[target] )
		else
			burstCount = 1

		for ( int index = 0; index < burstCount; index++ )
		{
			expandedStoredTargets.append( target )
		}
	}

	// Figure out the target we should be shooting this round
	entity target = null
	if ( expandedStoredTargets.len() > 0 )
	{
		int index = attackParams.burstIndex % expandedStoredTargets.len()
		target = expandedStoredTargets[index]
	}

	// Tried to shoot at a target but it's no longer valid. Don't do anything
	if ( target != null && !IsValid( target ) )
		return 0

	bool activeShot = false
	if ( target in targetActiveShots )
	{
		if ( targetActiveShots[target] )
		{
			activeShot = true
			damageType = damageType | DF_HEADSHOT
			explosionDamageType = explosionDamageType | DF_HEADSHOT
		}
	}

	// Fire the weapon
	string weaponType = weapon.GetSmartAmmoWeaponType()
	Assert( weaponType in VALID_WEAPON_TYPES )

	local shotsFired = VALID_WEAPON_TYPES[ weaponType ]( weapon, attackParams, damageType, explosionDamageType, activeShot, target )

	local isLastShot = ( attackParams.burstIndex + 1 == weapon.GetWeaponBurstFireCount() )
	if ( isLastShot )
	{
		if ( weaponRequiresTrackedMarks )
		{
			foreach ( target in storedTargets )
			{
				weapon.SmartAmmo_UntrackEntity( target )
			}
		}
		if ( SmartAmmo_GetUnlockAfterBurst( weapon ) )
		{
			weapon.SmartAmmo_Clear( true, false )
		}
	}

	return expect int( shotsFired )
}

int function SmartAmmo_FireWeapon_Bullet( entity weapon, WeaponPrimaryAttackParams attackParams, int damageType, int explosionDamageType, bool activeShot, entity target )
{
	if ( target )
	{
		// Shoot at the specified target
		Assert( IsValid( target ) )
		local dir = weapon.SmartAmmo_GetFirePosition( target, attackParams.burstIndex ) - attackParams.pos
		weapon.FireWeaponBullet_Special( attackParams.pos, dir, 1, damageType, true, true, false, false, false, activeShot, false )
	}
	else
	{
		// Not trying to shoot at a target, so just shoot straight
		weapon.FireWeaponBullet( attackParams.pos, attackParams.dir, 1, damageType )
	}

	return 1
}

int function SmartAmmo_FireWeapon_HomingMissile( entity weapon, WeaponPrimaryAttackParams attackParams, int damageType, int explosionDamageType, bool activeShot, entity target )
{
	local shouldPredict = weapon.ShouldPredictProjectiles()

	#if CLIENT
		if ( !shouldPredict )
			return 1
	#endif

	entity player = weapon.GetWeaponOwner()
	vector attackPos = attackParams.pos
	vector attackDir = attackParams.dir
	local missileSpeed = SmartAmmo_GetMissileSpeed( weapon )
	local doPopup = SmartAmmo_GetMissileShouldDropKick( weapon )

	if ( SmartAmmo_GetMissileAimAtCursor( weapon ) )
		attackDir = GetVectorFromPositionToCrosshair( player, attackPos )

	local homingSpeed = SmartAmmo_GetMissileHomingSpeed( weapon )
	local missileSpeedLimit = SmartAmmo_GetMissileSpeedLimit( weapon )

	array<entity> firedMissiles
	local missileFlightData = SmartAmmo_GetExpandContract( weapon )

	if ( missileFlightData == null )
	{
		entity missile = weapon.FireWeaponMissile( attackPos, attackDir, missileSpeed, damageType, explosionDamageType, doPopup, shouldPredict )
		if ( missile )
		{
			//InitMissileForRandomDrift( missile, attackPos, attackDir )
			missile.InitMissileForRandomDriftFromWeaponSettings( attackPos, attackDir )
			firedMissiles.append( missile )
		}
	}
	else
	{
		firedMissiles = FireExpandContractMissiles( weapon, attackParams, attackPos, attackDir, damageType, explosionDamageType, shouldPredict, expect int( missileFlightData.numRocketsPerShot ), missileSpeed, missileFlightData.launchOutAng, missileFlightData.launchOutTime, missileFlightData.launchInAng, missileFlightData.launchInTime, missileFlightData.launchInLerpTime, missileFlightData.launchStraightLerpTime, missileFlightData.applyRandSpread, weapon.GetWeaponBurstFireCount() )
	}

	foreach( missile in firedMissiles )
	{
		missile.kv.lifetime = 10

		#if SERVER
			missile.SetOwner( player )
			EmitSoundOnEntity( missile, HOMING_SFX_LOOP )
		#endif

		missile.SetSpeed( missileSpeed )
		missile.SetHomingSpeeds( homingSpeed, homingSpeed )
		SetTeam( missile, player.GetTeam() )

		if ( target )
		{
			if ( SmartAmmo_GetShouldTrackPosition( weapon ) )
				SmartAmmo_SetMissileTargetPosition( missile, target )
			else
				SmartAmmo_SetMissileTarget( missile, target )
		}

		if ( "missileThinkThread" in weapon.s )
			thread weapon.s.missileThinkThread( weapon, missile )
	}

	return firedMissiles.len()
}

/******************************************/
/* 	 GLOBAL - WEAPON SETTINGS OVERRIDES   */
/******************************************/

function SmartAmmo_SetWeaponSettingOverride( weapon, setting, value )
{
	if ( !( setting in weapon.s ) )
		weapon.s[ setting ] <- null
	weapon.s[ setting ] = value
}

function SmartAmmo_GetWeaponSettingOverride( weapon, setting, defaultValue = null )
{
	if ( setting in weapon.s )
		return weapon.s[ setting ]
	return defaultValue
}

function SmartAmmo_GetUseAltAmmoFunc( weapon )
{
	return SmartAmmo_GetWeaponSettingOverride( weapon, "useAltAmmoFunc" )
}

function SmartAmmo_SetWarningIndicatorDelay( weapon, float warningIndicatorDelay )
{
	SmartAmmo_SetWeaponSettingOverride( weapon, "warningIndicatorDelay", warningIndicatorDelay )
}

function SmartAmmo_GetWarningIndicatorDelay( weapon )
{
	return SmartAmmo_GetWeaponSettingOverride( weapon, "warningIndicatorDelay", 0.0 )
}

function SmartAmmo_SetAllowUnlockedFiring( weapon, allow = true )
{
	SmartAmmo_SetWeaponSettingOverride( weapon, "allowUnlockedFiring", allow )
}

function SmartAmmo_GetAllowUnlockedFiring( weapon )
{
	return SmartAmmo_GetWeaponSettingOverride( weapon, "allowUnlockedFiring", false )
}

function SmartAmmo_SetWeaponFireFailedTime( weapon )
{
	SmartAmmo_SetWeaponSettingOverride( weapon, "lastFireWeaponFailTime", Time() )
}

function SmartAmmo_GetWeaponFireFailedTime( weapon )
{
	if ( !( "lastFireWeaponFailTime" in weapon.s ) )
		weapon.s.lastFireWeaponFailTime <- -1
	return Time() - weapon.s.lastFireWeaponFailTime
}

function SmartAmmo_SetAimAttachment( weapon, targetAttachment )
{
	SmartAmmo_SetWeaponSettingOverride( weapon, "targetAttachment", targetAttachment )
}

function SmartAmmo_GetAimAttachment( weapon, ent )
{
	if( ent.IsPlayer())
	{
		return SmartAmmo_GetWeaponSettingOverride( weapon, "targetAttachment", SMART_AMMO_DEFAULT_AIM_ATTACHMENT )
	}
	else if( ent.GetClassName() == "grenade_frag" )
	{
		return SmartAmmo_GetWeaponSettingOverride( weapon, "targetAttachment", SMART_AMMO_GRENADE_AIM_ATTACHMENT )
	}
	else
	{
		return SmartAmmo_GetWeaponSettingOverride( weapon, "targetAttachment", SMART_AMMO_AI_AIM_ATTACHMENT )
	}
}

function SmartAmmo_SetShouldTrackPosition( weapon, shouldTrackPosition )
{
	SmartAmmo_SetWeaponSettingOverride( weapon, "shouldTrackPosition", shouldTrackPosition )
}

function SmartAmmo_GetShouldTrackPosition( weapon )
{
	return SmartAmmo_GetWeaponSettingOverride( weapon, "shouldTrackPosition", false )
}

function SmartAmmo_SetMissileSpeed( weapon, missileSpeed )
{
	SmartAmmo_SetWeaponSettingOverride( weapon, "missileSpeed", missileSpeed )
}

function SmartAmmo_GetMissileSpeed( weapon )
{
	return SmartAmmo_GetWeaponSettingOverride( weapon, "missileSpeed", 2500.0 )
}

function SmartAmmo_SetMissileHomingSpeed( weapon, missileHomingSpeed )
{
	SmartAmmo_SetWeaponSettingOverride( weapon, "missileHomingSpeed", missileHomingSpeed )
}

function SmartAmmo_GetMissileHomingSpeed( weapon )
{
	return SmartAmmo_GetWeaponSettingOverride( weapon, "missileHomingSpeed", 300 )
}

function SmartAmmo_SetMissileShouldDropKick( weapon, doDropKick = false )
{
	SmartAmmo_SetWeaponSettingOverride( weapon, "missilesShouldDropKick", doDropKick )
}

function SmartAmmo_GetMissileShouldDropKick( weapon )
{
	return SmartAmmo_GetWeaponSettingOverride( weapon, "missilesShouldDropKick", false )
}

function SmartAmmo_SetMissileSpeedLimit( weapon, missileSpeedLimit = 0 )
{
	SmartAmmo_SetWeaponSettingOverride( weapon, "missileSpeedLimit", missileSpeedLimit )
}

function SmartAmmo_GetMissileSpeedLimit( weapon )
{
	return SmartAmmo_GetWeaponSettingOverride( weapon, "missileSpeedLimit", 0 )
}

function SmartAmmo_SetUnlockAfterBurst( weapon, unlockAfterBurst )
{
	SmartAmmo_SetWeaponSettingOverride( weapon, "unlockAfterBurst", unlockAfterBurst )
}

function SmartAmmo_GetUnlockAfterBurst( weapon )
{
	return SmartAmmo_GetWeaponSettingOverride( weapon, "unlockAfterBurst", false )
}

function SmartAmmo_SetMissileAimAtCursor( weapon, aimAtCenter )
{
	SmartAmmo_SetWeaponSettingOverride( weapon, "aimAtCenter", aimAtCenter )
}

function SmartAmmo_GetMissileAimAtCursor( weapon )
{
	return SmartAmmo_GetWeaponSettingOverride( weapon, "aimAtCenter", false )
}

function SmartAmmo_SetDisplayKeybinding( weapon, displayKeybinding )
{
	SmartAmmo_SetWeaponSettingOverride( weapon, "displayKeybinding", displayKeybinding )
}

function SmartAmmo_GetDisplayKeybinding( weapon )
{
	return SmartAmmo_GetWeaponSettingOverride( weapon, "displayKeybinding", true )
}

function SmartAmmo_SetExpandContract( weapon, numRocketsPerShot, applyRandSpread, launchOutAng, launchOutTime, launchInLerpTime, launchInAng, launchInTime, launchStraightLerpTime )
{
	SmartAmmo_SetWeaponSettingOverride( weapon, "exmissile_numRocketsPerShot", numRocketsPerShot )
	SmartAmmo_SetWeaponSettingOverride( weapon, "exmissile_applyRandSpread", applyRandSpread )
	SmartAmmo_SetWeaponSettingOverride( weapon, "exmissile_launchOutAng", launchOutAng )
	SmartAmmo_SetWeaponSettingOverride( weapon, "exmissile_launchOutTime", launchOutTime )
	SmartAmmo_SetWeaponSettingOverride( weapon, "exmissile_launchInLerpTime", launchInLerpTime )
	SmartAmmo_SetWeaponSettingOverride( weapon, "exmissile_launchInAng", launchInAng )
	SmartAmmo_SetWeaponSettingOverride( weapon, "exmissile_launchInTime", launchInTime )
	SmartAmmo_SetWeaponSettingOverride( weapon, "exmissile_launchStraightLerpTime", launchStraightLerpTime )
}

function SmartAmmo_GetExpandContract( weapon )
{
	local data = {}
	data.numRocketsPerShot <- SmartAmmo_GetWeaponSettingOverride( weapon, "exmissile_numRocketsPerShot" )
	data.applyRandSpread <- SmartAmmo_GetWeaponSettingOverride( weapon, "exmissile_applyRandSpread" )
	data.launchOutAng <- SmartAmmo_GetWeaponSettingOverride( weapon, "exmissile_launchOutAng" )
	data.launchOutTime <- SmartAmmo_GetWeaponSettingOverride( weapon, "exmissile_launchOutTime" )
	data.launchInLerpTime <- SmartAmmo_GetWeaponSettingOverride( weapon, "exmissile_launchInLerpTime" )
	data.launchInAng <- SmartAmmo_GetWeaponSettingOverride( weapon, "exmissile_launchInAng" )
	data.launchInTime <- SmartAmmo_GetWeaponSettingOverride( weapon, "exmissile_launchInTime" )
	data.launchStraightLerpTime <- SmartAmmo_GetWeaponSettingOverride( weapon, "exmissile_launchStraightLerpTime" )
	foreach( k, v in data )
	{
		if ( v == null )
			return null
	}
	return data
}

/*******************************/
/* 	 GLOBAL UTILITY FUNCTION   */
/*******************************/

function SmartAmmo_SetMissileTarget( missile, target )
{
	if ( !IsValid( missile ) || !IsValid( target ) )
		return

	// Set the missile locking offset
	//local missileTargetOffset = target.IsTitan() ? Vector( 0, 0, -25 ) : Vector( 0, 0, -25 )

	// Set the missile target and homing speed
	missile.SetMissileTarget( target, Vector( 0, 0, 0 ) )
}

void function SmartAmmo_SetMissileTargetPosition( entity missile, entity target )
{
	if ( !IsValid( missile ) || !IsValid( target ) )
		return

	// Set the missile locking offset
	//local missileTargetOffset = target.IsTitan() ? Vector( 0, 0, -25 ) : Vector( 0, 0, -25 )

	// Set the missile target and homing speed
	missile.SetMissileTargetPosition( target.EyePosition() )
}

void function SmartAmmo_TransferMissileLockons( entity soul, entity titan, entity oldTitan )
{
	if ( !IsValid( oldTitan ) || !IsValid( titan ) )
		return

	local missiles = GetProjectileArray()
	foreach( missile in missiles )
	{
		if ( missile.GetClassName() != "rpg_missile" )
			continue
		if ( !( "GetMissileTarget" in missile ) )
			continue
		if ( missile.GetMissileTarget() == oldTitan )
			SmartAmmo_SetMissileTarget( missile, titan )
	}
}

bool function SmartAmmo_CanWeaponBeFired( entity weapon )
{
	Assert( weapon.SmartAmmo_IsEnabled() )

	if ( !weapon.IsReadyToFire() || weapon.IsReloading() )
		return false

	// Weapon is ready to fire but it's a smart weapon so we have some additional checks.
	// If the weapon requires a lock to be fired then we make sure the weapon has a full lock
	if ( SmartAmmo_GetAllowUnlockedFiring( weapon ) == true )
		return true

	local targets = weapon.SmartAmmo_GetTargets()
	local highestFraction = 0

	bool weaponRequiresTrackedMarks = SmartAmmo_WeaponRequiresTrackedMarks( weapon )

	foreach( target in targets )
	{
		if ( weaponRequiresTrackedMarks && !SmartAmmo_EntHasEnoughTrackedMarks( weapon, expect entity( target.ent ) ) )
			continue

		if ( target.fraction > highestFraction )
			highestFraction = target.fraction
	}

	return highestFraction == 1.0
}

/*************************/
/* 	 INTERNAL FUNCTION   */
/*************************/

#if CLIENT
/*
void function ClientCodeCallback_OnPredictedEntityRemove( entity serverEntity, entity predictedEntity )
{
}
*/
#endif // CLIENT

#if SERVER
void function SmartPistol_DamagedPlayerOrNPC( entity ent, var damageInfo )
{
	if ( IsSingleplayer() )
		return

	if ( !IsValid( ent ) )
		return

	int maxHealth = ent.GetMaxHealth()
	float damage = DamageInfo_GetDamage( damageInfo )

	if ( IsSpectre( ent ) )
	{
		DamageInfo_SetDamage( damageInfo, (maxHealth / 2) + 1 )
	}
	else if ( IsGrunt( ent ) )
	{
		if ( damage < maxHealth )
			DamageInfo_SetDamage( damageInfo, maxHealth )
	}

}
#endif // SERVER

void function RemovePartiallyTrackedEnts( entity weapon, array<entity> targets )
{
		for ( int i = targets.len() - 1; i >= 0; i-- )
		{
			if ( !SmartAmmo_EntHasEnoughTrackedMarks( weapon, targets[i] ) )
				targets.remove( i )
		}
}

bool function SmartAmmo_WeaponRequiresTrackedMarks( entity weapon )
{
	return weapon.GetWeaponSettingInt( eWeaponVar.smart_ammo_max_trackers_per_target ) > 1
}

bool function SmartAmmo_EntHasEnoughTrackedMarks( entity weapon, entity target )
{
	int numTrackers = weapon.SmartAmmo_GetNumTrackersOnEntity( target )
	int maxTrackers = weapon.GetWeaponSettingInt( eWeaponVar.smart_ammo_max_trackers_per_target )
	Assert( numTrackers <= maxTrackers )
	return numTrackers == maxTrackers
}

array<entity> function SmartAmmo_GetWeaponTargets( entity weapon )
{
	var targets = weapon.SmartAmmo_GetTargets()
	array<entity> validTargets = []

	foreach ( target in targets )
	{
		if ( target.fraction < 1.0 )
			continue

		entity ent = expect entity( target.ent )
		validTargets.append( ent )
	}

	return validTargets
}